<?xml version="1.0"?>
<!-- *************************************************************************

   gEDA - GPL Electronic Design Automation
   gparts - gEDA Parts Manager
   Copyright (C) 2010 Edward C. Hennessy
   Copyright (C) 2010 gEDA Contributors (see ChangeLog for details)
  
   This program is free software; you can redistribute it and/or modify
   it under the terms of the GNU General Public License as published by
   the Free Software Foundation; either version 2 of the License, or
   (at your option) any later version.
  
   This program is distributed in the hope that it will be useful,
   but WITHOUT ANY WARRANTY; without even the implied warranty of
   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
   GNU General Public License for more details.
  
   You should have received a copy of the GNU General Public License
   along with this program; if not, write to the Free Software
   Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111 USA

******************************************************************************

   An XSLT stylesheet to convert XML import data into SQL suitable for
   import into a SQLite database.

************************************************************************** -->

<xsl:stylesheet version = "1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform">

    <xsl:output method="text"/>

    <xsl:template match="/">
        <xsl:apply-templates select="data/company"/>
        <xsl:apply-templates select="data/package"/>
        <xsl:apply-templates select="data/resistor"/>
        <xsl:apply-templates select="data/bipolar-transistor"/>
        <xsl:apply-templates select="data/zener-diode"/>
    </xsl:template>

    <!-- Add a bipolar transistor to the database -->

    <xsl:template match="bipolar-transistor">
        <xsl:apply-templates select="company-name"/>
        <xsl:apply-templates select="device-name"/>
        <xsl:apply-templates select="package-name"/>

        <xsl:call-template name="part"/>

        <xsl:text>INSERT INTO BJT (PartID, PackageID, Polarity, IC, FT, PD)&#10;</xsl:text>
        <xsl:text>    SELECT&#10;</xsl:text>
        <xsl:text>        (SELECT PartID FROM Part WHERE PartNumber = '</xsl:text>
        <xsl:value-of select="part-number"/>
        <xsl:text>'),&#10;</xsl:text>
        <xsl:text>        (SELECT PackageID FROM Package WHERE PackageName = '</xsl:text>
        <xsl:value-of select="package-name"/>
        <xsl:text>'),&#10;</xsl:text>
        <xsl:text>        '</xsl:text>
        <xsl:value-of select="polarity"/>
        <xsl:text>',&#10;</xsl:text>
        <xsl:text>        </xsl:text>
        <xsl:value-of select="collector-current"/>
        <xsl:text>,&#10;</xsl:text>
        <xsl:text>        </xsl:text>
        <xsl:value-of select="transition-frequency"/>
        <xsl:text>,&#10;</xsl:text>
        <xsl:text>        </xsl:text>
        <xsl:value-of select="power-dissipation"/>
        <xsl:text>&#10;</xsl:text>
        <xsl:text>    WHERE NOT EXISTS&#10;</xsl:text>
        <xsl:text>        (SELECT * FROM BJT&#10;</xsl:text>
        <xsl:text>             JOIN Part USING ( PartID )&#10;</xsl:text>
        <xsl:text>             JOIN Company USING ( CompanyID )&#10;</xsl:text>
        <xsl:text>             WHERE CompanyName = '</xsl:text>
        <xsl:value-of select="company-name"/>
        <xsl:text>' AND PartNumber = '</xsl:text>
        <xsl:value-of select="part-number"/>
        <xsl:text>'&#10;</xsl:text>
        <xsl:text>             );&#10;&#10;</xsl:text>

    </xsl:template>


    <!-- Add a company to the database -->

    <xsl:template match="company">
        <xsl:apply-templates select="company-name"/>

        <xsl:text>UPDATE Company&#10;</xsl:text>
        <xsl:text>    SET Website = '</xsl:text>
        <xsl:value-of select="website"/>
        <xsl:text>'&#10;</xsl:text>
        <xsl:text>    WHERE CompanyName = '</xsl:text>
        <xsl:value-of select="company-name"/>
        <xsl:text>';&#10;&#10;</xsl:text>
    </xsl:template>

    <!-- Ensure a company exists in the database -->

    <xsl:template match="company-name">
        <xsl:text>INSERT INTO Company ( CompanyName )&#10;</xsl:text>
        <xsl:text>    SELECT&#10;</xsl:text>
        <xsl:text>        '</xsl:text>
        <xsl:value-of select="."/>
        <xsl:text>'&#10;</xsl:text>
        <xsl:text>    WHERE NOT EXISTS&#10;</xsl:text>
        <xsl:text>        (SELECT * FROM Company WHERE CompanyName = '</xsl:text>
        <xsl:value-of select="."/>
        <xsl:text>');&#10;&#10;</xsl:text>
    </xsl:template>

    <!-- Ensure a device exists in the database -->

    <xsl:template match="device-name">
        <xsl:text>INSERT INTO Device ( DeviceName )&#10;</xsl:text>
        <xsl:text>    SELECT&#10;</xsl:text>
        <xsl:text>        '</xsl:text>
        <xsl:value-of select="."/>
        <xsl:text>'&#10;</xsl:text>
        <xsl:text>    WHERE NOT EXISTS&#10;</xsl:text>
        <xsl:text>        (SELECT * FROM Device WHERE DeviceName = '</xsl:text>
        <xsl:value-of select="."/>
        <xsl:text>');&#10;&#10;</xsl:text>
    </xsl:template>

    <!-- Add a document to the database -->

    <xsl:template match="document">
        <xsl:text>INSERT INTO Document ( Category, SourceURL )&#10;</xsl:text>
        <xsl:text>    SELECT&#10;</xsl:text>
        <xsl:text>        '</xsl:text>
        <xsl:value-of select="./@category"/>
        <xsl:text>',&#10;</xsl:text>
        <xsl:text>        '</xsl:text>
        <xsl:value-of select="."/>
        <xsl:text>'&#10;</xsl:text>
        <xsl:text>    WHERE NOT EXISTS&#10;</xsl:text>
        <xsl:text>        (SELECT * FROM Document WHERE SourceURL = '</xsl:text>
        <xsl:value-of select="."/>
        <xsl:text>');&#10;&#10;</xsl:text>

        <xsl:text>INSERT INTO DocumentPartJ ( DocumentID, PartID )&#10;</xsl:text>
        <xsl:text>    SELECT&#10;</xsl:text>
        <xsl:text>        (SELECT DocumentID FROM Document WHERE SourceURL = '</xsl:text>
        <xsl:value-of select="."/>
        <xsl:text>'),&#10;</xsl:text>
        <xsl:text>        (SELECT PartID FROM Part JOIN Company USING ( CompanyID ) WHERE&#10;</xsl:text>
        <xsl:text>            CompanyName = '</xsl:text>
        <xsl:value-of select="../company-name"/>
        <xsl:text>' AND&#10;</xsl:text>
        <xsl:text>            PartNumber = '</xsl:text>
        <xsl:value-of select="../part-number"/>
        <xsl:text>')&#10;</xsl:text>
        <xsl:text>    WHERE NOT EXISTS&#10;</xsl:text>
        <xsl:text>        (SELECT * FROM DocumentPartJ WHERE&#10;</xsl:text>
        <xsl:text>            DocumentID = (SELECT DocumentID FROM Document WHERE SourceURL = '</xsl:text>
        <xsl:value-of select="."/>
        <xsl:text>') AND&#10;</xsl:text>
        <xsl:text>            PartID = (SELECT PartID FROM Part JOIN Company USING ( CompanyID ) WHERE&#10;</xsl:text>
        <xsl:text>                CompanyName = '</xsl:text>
        <xsl:value-of select="../company-name"/>
        <xsl:text>' AND&#10;</xsl:text>
        <xsl:text>                PartNumber = '</xsl:text>
        <xsl:value-of select="../part-number"/>
        <xsl:text>'));&#10;&#10;</xsl:text>
    </xsl:template>


    <!-- Ensure a package exists in the database -->

    <xsl:template match="package-name">
        <xsl:text>INSERT INTO Package ( PackageName )&#10;</xsl:text>
        <xsl:text>    SELECT&#10;</xsl:text>
        <xsl:text>        '</xsl:text>
        <xsl:value-of select="."/>
        <xsl:text>'&#10;</xsl:text>
        <xsl:text>    WHERE NOT EXISTS&#10;</xsl:text>
        <xsl:text>        (SELECT * FROM Package WHERE PackageName = '</xsl:text>
        <xsl:value-of select="."/>
        <xsl:text>');&#10;&#10;</xsl:text>
    </xsl:template>

    <!-- Add a package to the database -->

    <xsl:template match="package">
        <xsl:text>INSERT INTO Package ( PackageName, Technology )&#10;</xsl:text>
        <xsl:text>    SELECT&#10;</xsl:text>
        <xsl:text>        '</xsl:text>
        <xsl:value-of select="package-name"/>
        <xsl:text>',&#10;</xsl:text>
        <xsl:text>        '</xsl:text>
        <xsl:value-of select="technology"/>
        <xsl:text>'&#10;</xsl:text>
        <xsl:text>    WHERE NOT EXISTS&#10;</xsl:text>
        <xsl:text>        ( SELECT * FROM Package WHERE PackageName = '</xsl:text>
        <xsl:value-of select="package-name"/>
        <xsl:text>');&#10;&#10;</xsl:text>
    </xsl:template>

    <xsl:template name="part">
        <xsl:text>INSERT INTO Part (CompanyID, PartNumber, DeviceID)&#10;</xsl:text>
        <xsl:text>    SELECT &#10;</xsl:text>
        <xsl:text>        (SELECT CompanyID FROM Company WHERE CompanyName = '</xsl:text>
        <xsl:value-of select="company-name"/>
        <xsl:text>'),&#10;</xsl:text>
        <xsl:text>        '</xsl:text>
        <xsl:value-of select="part-number"/>
        <xsl:text>',&#10;</xsl:text>
        <xsl:text>        (SELECT DeviceID FROM Device WHERE DeviceName = '</xsl:text>
        <xsl:value-of select="device-name"/>
        <xsl:text>')&#10;</xsl:text>
        <xsl:text>    WHERE NOT EXISTS&#10;</xsl:text>
        <xsl:text>        (SELECT * FROM Part&#10;</xsl:text>
        <xsl:text>            JOIN Company USING ( CompanyID )&#10;</xsl:text>
        <xsl:text>        WHERE CompanyName = '</xsl:text>
        <xsl:value-of select="company-name"/>
        <xsl:text>' AND PartNumber = '</xsl:text>
        <xsl:value-of select="part-number"/>
        <xsl:text>'&#10;</xsl:text>
        <xsl:text>        );&#10;&#10;</xsl:text>

        <xsl:apply-templates select="document"/>
    </xsl:template>

    <!-- Add a resistor to the database -->

    <xsl:template match="resistor">
        <xsl:apply-templates select="company-name"/>
        <xsl:apply-templates select="device-name"/>
        <xsl:apply-templates select="package-name"/>

        <xsl:call-template name="part"/>

        <xsl:text>INSERT INTO Resistor (PartID, PackageID, Resistance, Tolerance)&#10;</xsl:text>
        <xsl:text>    SELECT&#10;</xsl:text>
        <xsl:text>        (SELECT PartID FROM Part WHERE PartNumber = '</xsl:text>
        <xsl:value-of select="part-number"/>
        <xsl:text>'),&#10;</xsl:text>
        <xsl:text>        (SELECT PackageID FROM Package WHERE PackageName = '</xsl:text>
        <xsl:value-of select="package-name"/>
        <xsl:text>'),&#10;</xsl:text>
        <xsl:text>        </xsl:text>
        <xsl:value-of select="resistance"/>
        <xsl:text>,&#10;</xsl:text>
        <xsl:text>        </xsl:text>
        <xsl:value-of select="tolerance"/>
        <xsl:text>&#10;</xsl:text>
        <xsl:text>    WHERE NOT EXISTS&#10;</xsl:text>
        <xsl:text>        (SELECT * FROM Resistor&#10;</xsl:text>
        <xsl:text>             JOIN Part USING ( PartID )&#10;</xsl:text>
        <xsl:text>             JOIN Company USING ( CompanyID )&#10;</xsl:text>
        <xsl:text>             WHERE CompanyName = '</xsl:text>
        <xsl:value-of select="company-name"/>
        <xsl:text>' AND PartNumber = '</xsl:text>
        <xsl:value-of select="part-number"/>
        <xsl:text>'&#10;</xsl:text>
        <xsl:text>             );&#10;&#10;</xsl:text>
    </xsl:template>


    <!-- Add a zener diode to the database -->

    <xsl:template match="zener-diode">
        <xsl:apply-templates select="company-name"/>
        <xsl:apply-templates select="device-name"/>
        <xsl:apply-templates select="package-name"/>

        <xsl:call-template name="part"/>

        <xsl:text>INSERT INTO ZenerDiode (PartID, PackageID, ZenerVoltage)&#10;</xsl:text>
        <xsl:text>    SELECT&#10;</xsl:text>
        <xsl:text>        (SELECT PartID FROM Part WHERE PartNumber = '</xsl:text>
        <xsl:value-of select="part-number"/>
        <xsl:text>'),&#10;</xsl:text>
        <xsl:text>        (SELECT PackageID FROM Package WHERE PackageName = '</xsl:text>
        <xsl:value-of select="package-name"/>
        <xsl:text>'),&#10;</xsl:text>
        <xsl:text>        </xsl:text>
        <xsl:value-of select="zener-voltage"/>
        <xsl:text>&#10;</xsl:text>
        <xsl:text>    WHERE NOT EXISTS&#10;</xsl:text>
        <xsl:text>        (SELECT * FROM ZenerDiode&#10;</xsl:text>
        <xsl:text>             JOIN Part USING ( PartID )&#10;</xsl:text>
        <xsl:text>             JOIN Company USING ( CompanyID )&#10;</xsl:text>
        <xsl:text>             WHERE CompanyName = '</xsl:text>
        <xsl:value-of select="company-name"/>
        <xsl:text>' AND PartNumber = '</xsl:text>
        <xsl:value-of select="part-number"/>
        <xsl:text>'&#10;</xsl:text>
        <xsl:text>             );&#10;&#10;</xsl:text>
    </xsl:template>

</xsl:stylesheet>

